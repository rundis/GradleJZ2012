apply plugin: 'base'

task compileJava() {
	ext.srcDir = file('src')
	ext.classesDir = file('build/classes')
	
	inputs.files srcDir
	outputs.files classesDir
	
	doFirst {
		if(!classesDir.exists()) classesDir.mkdirs()
	}
	doLast {
		ant.javac(srcdir: srcDir, destdir: classesDir, includeAntRuntime:false)
	}
}


task jar() {
	ext.libsDir = file("build/libs")
	ext.archivePath = file("build/libs/${project.name}.jar")
	
	inputs.files compileJava.outputs.files
	outputs.files archivePath
	
	doFirst {
		if(!libsDir.exists()) libsDir.mkdirs()
	}
	doLast {
		ant.jar(basedir: compileJava.classesDir, destfile: archivePath)
	}
}



task run(type: JavaExec) {
	inputs.files jar.outputs.files
	main 'no.gradle.javazone.IncrementalDemo'
	classpath jar.archivePath
}